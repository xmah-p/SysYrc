grammar;

use crate::ast::*;

// grammar rules:
// CompUnit    ::= FuncDef;

// Decl          ::= ConstDecl | VarDecl;
// ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
// BType         ::= "int";
// ConstDef      ::= IDENT "=" ConstInitVal;
// ConstInitVal  ::= ConstExp;
// ConstExp    ::= Exp;
// VarDecl       ::= BType VarDef {"," VarDef} ";";
// VarDef        ::= IDENT ["=" InitVal];
// InitVal       ::= Exp;

// FuncDef     ::= FuncType IDENT "(" ")" Block;
// FuncType    ::= "int";

// Block         ::= "{" {BlockItem} "}";
// BlockItem     ::= Decl | Stmt;

// Stmt        ::= "return" [Exp] ";";
//                 | LVal "=" Exp ";";
//                 | [Exp] ";";
//                 | Block;

// Exp         ::= LOrExp;
// PrimaryExp  ::= "(" Exp ")" | Number;
// PrimaryExp    ::= "(" Exp ")" | LVal | Number;
// Number      ::= INT_CONST;
// UnaryExp    ::= PrimaryExp | UnaryOp UnaryExp;
// UnaryOp     ::= "+" | "-" | "!";
// MulExp      ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
// AddExp      ::= MulExp | AddExp ("+" | "-") MulExp;
// RelExp      ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
// EqExp       ::= RelExp | EqExp ("==" | "!=") RelExp;
// LAndExp     ::= EqExp | LAndExp "&&" EqExp;
// LOrExp      ::= LAndExp | LOrExp "||" LAndExp;

// LVal          ::= IDENT;


// lexer rules
match {
    r"\s*" => {},                // Ignore whitespace
    r"//[^\n\r]*[\n\r]*" => {},  // Ignore single-line comments
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {}, // Ignore multi-line comments
    _                            // Default case: convert to token and pass to parser
}


// Non-teminal: Return Type = Match Pattern => Action Code
pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <func_name: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
    // This is a macro provided by LALRPOP that automatically expands the names 
    // of the captures in the rule into fields of the struct.
    // Equivalent to:
    // FuncDef {
    //   func_type,
    //   func_name,
    //   block,
    // }
  }
}
FuncType: FuncType = "int" => FuncType::Int;

// ==============================================
// Block
// ==============================================

// `*` returns a `Vec`
Block: Block = "{" <items: BlockItem*> "}" => Block { 
    items: items.into_iter().flatten().collect() 
};

BlockItem: Vec<BlockItem> = {
    <decl: Decl> => decl, 
    <stmt: Stmt> => vec![BlockItem::Stmt(stmt)], 
}

Decl: Vec<BlockItem> = {
    <c: ConstDecl> => c,
    <v: VarDecl> => v,
}

ConstDecl: Vec<BlockItem> = {
    "const" <vt: VType> <defs: Comma<ConstDef>> ";" => {
        defs.into_iter()
            .map(|(name, init)| BlockItem::Decl( {Decl {
                constant: true,
                var_type: vt.clone(), 
                var_name: name,
                init_expr: Some(init),
            }}))
            .collect()
    }
}

VarDecl: Vec<BlockItem> = {
    <vt: VType> <defs: Comma<VarDef>> ";" => {
        defs.into_iter()
            .map(|(name, init)| BlockItem::Decl( {Decl {
                constant: false,
                var_type: vt.clone(), 
                var_name: name,
                init_expr: init,
            }}))
            .collect()
    }
}

VType: ValueType = {
    "int" => ValueType::Int,
}


ConstDef: (String, Expr) = {
    <id: Ident> "=" <val: ConstInitVal> => (id, val),
}

ConstInitVal: Expr = {
    <e: ConstExpr> => e,
}

ConstExpr: Expr = {
    <e: Expr> => e,
}

VarDef: (String, Option<Expr>) = {
    <id: Ident> "=" <val: InitVal> => (id, Some(val)),
    <id: Ident> => (id, None),
}

InitVal: Expr = {
    <e: Expr> => e,
}

Comma<T>: Vec<T> = { 
    <mut v: (<T> ",")*> <e: T> => {
        v.push(e);
        v
    }
};

// ==============================================
// Statement
// ==============================================

Stmt: Stmt = {
    MatchedStmt,
    OpenStmt,
}

MatchedStmt: Stmt = {
    "return" <expr: Expr> ";" => Stmt::Return { expr: Some(expr) },
    "return" ";" => Stmt::Return { expr: None },
    <lval: LVal> "=" <expr: Expr> ";" => Stmt::Assign {
        lval: lval,
        expr: expr,
    },
    ";" => Stmt::Expression { expr: None },
    <expr: Expr> ";" => Stmt::Expression { expr: Some(expr) },
    <block: Block> => Stmt::Block { block: block },
    "if" "(" <cond: Expr> ")" <then_body: MatchedStmt> "else" <else_body: MatchedStmt> => Stmt::If {
        cond,
        then_body: Box::new(then_body),
        else_body: Some(Box::new(else_body)),
    },
};

OpenStmt: Stmt = {
    // Case A: No else
    // Only when there's no else, we can allow then_body to be OpenStmt
    "if" "(" <cond: Expr> ")" <then_body: Stmt> => {
        Stmt::If {
            cond,
            then_body: Box::new(then_body),
            else_body: None,
        }
    },

    // Case B: With else, but else_body is OpenStmt
    // then_body must be MatchedStmt here, otherwise else would be parsed as belonging to then_body
    // instead of the current if.
    "if" "(" <cond: Expr> ")" <then_body: MatchedStmt> "else" <else_body: OpenStmt> => {
        Stmt::If {
            cond,
            then_body: Box::new(then_body),
            else_body: Some(Box::new(else_body)),
        }
    }
}

// ==============================================
// Expression
// ==============================================
Expr: Expr = LOrExpr;

LOrExpr: Expr = {
    <lhs: LOrExpr> "||" <rhs: LAndExpr> => Expr::Binary {
        op: BinaryOp::Or,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    LAndExpr,
};

LAndExpr: Expr = {
    <lhs: LAndExpr> "&&" <rhs: EqExpr> => Expr::Binary {
        op: BinaryOp::And,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    EqExpr,
};

EqExpr: Expr = {
    <lhs: EqExpr> "==" <rhs: RelExpr> => Expr::Binary {
        op: BinaryOp::Eq,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: EqExpr> "!=" <rhs: RelExpr> => Expr::Binary {
        op: BinaryOp::Neq,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    RelExpr,
};

RelExpr: Expr = {
    <lhs: RelExpr> "<" <rhs: AddExpr> => Expr::Binary {
        op: BinaryOp::Lt,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: RelExpr> ">" <rhs: AddExpr> => Expr::Binary {
        op: BinaryOp::Gt,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: RelExpr> "<=" <rhs: AddExpr> => Expr::Binary {
        op: BinaryOp::Leq,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: RelExpr> ">=" <rhs: AddExpr> => Expr::Binary {
        op: BinaryOp::Geq,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    AddExpr,
};

AddExpr: Expr = {
    <lhs: AddExpr> "+" <rhs: MulExpr> => Expr::Binary {
        op: BinaryOp::Add,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: AddExpr> "-" <rhs: MulExpr> => Expr::Binary {
        op: BinaryOp::Sub,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    MulExpr,
};

MulExpr: Expr = {
    <lhs: MulExpr> "*" <rhs: UnaryExpr> => Expr::Binary {
        op: BinaryOp::Mul,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: MulExpr> "/" <rhs: UnaryExpr> => Expr::Binary {
        op: BinaryOp::Div,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    <lhs: MulExpr> "%" <rhs: UnaryExpr> => Expr::Binary {
        op: BinaryOp::Mod,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
    },
    UnaryExpr,
};

UnaryExpr: Expr = {
    <op: UnaryOp> <expr: UnaryExpr> => Expr::Unary {
        op: op,
        expr: Box::new(expr),
    },
    PrimaryExpr,
};

PrimaryExpr: Expr = {
    "(" <Expr> ")",
    <lval: LVal> => Expr::LVal(lval),
    <num:Number> => Expr::Number(num),
}

UnaryOp: UnaryOp = {
    "+" => UnaryOp::Pos,
    "-" => UnaryOp::Neg,
    "!" => UnaryOp::Not,
};

BinaryOp: BinaryOp = {
    "||" => BinaryOp::Or,
    "&&" => BinaryOp::And,
    "==" => BinaryOp::Eq,
    "!=" => BinaryOp::Neq,
    "<"  => BinaryOp::Lt,
    ">"  => BinaryOp::Gt,
    "<=" => BinaryOp::Leq,
    ">=" => BinaryOp::Geq,
    "+"  => BinaryOp::Add,
    "-"  => BinaryOp::Sub,
    "*"  => BinaryOp::Mul,
    "/"  => BinaryOp::Div,
    "%"  => BinaryOp::Mod,
};

LVal: String = <name: Ident> => <>;

Number: i32 = <num: IntConst> => <>;

// <> stands for the matched text slice &str
// to_string() converts this &str to an owned String
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

IntConst: i32 = {
    r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),                // decimal
    r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),                     // octal
    r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),    // hexadecimal
}
